/* -*- java -*-

This software is OSI Certified Open Source Software.
OSI Certified is a certification mark of the Open Source Initiative.

The license (Mozilla version 1.0) can be read at the MMBase site.
See http://www.MMBase.org/license

*/
/**
 * DateParsed inspired by at(1) syntax, and original mm:time.
 * This file is parsed by <a href="https://javacc.dev.java.net/">javacc</a>.
 *
 * @author Michiel Meeuwisssen
 * @since MMBase-1-8
 */

options {
    LOOKAHEAD = 2;
    STATIC = false;
    FORCE_LA_CHECK = false;
    IGNORE_CASE = true;
}

PARSER_BEGIN(DateParser)

package org.mmbase.util.dateparser;

import java.util.*;
import java.io.*;


public class DateParser {

    private static Random random = new Random();
    public static TimeZone defaultTimeZone = TimeZone.getDefault();

    public static void setDefault(TimeZone tz) {
        defaultTimeZone = tz;
    }
    private boolean dynamic = true;
    private long prev = 0;
    private int amount = 0;
    private int field = 0;
    private Calendar date = Calendar.getInstance(defaultTimeZone);

    public static void main(String argv[]) throws ParseException, InterruptedException {
        DateParser parser = new DateParser(new StringReader(argv[0]));
        parser.start();
        System.out.println("" + parser.toDate());
    }
    public Date toDate()  {
        return date.getTime();
    }
    public boolean dynamic()  {
        return dynamic;
    }
    protected void beginOfHour() {
        date.set(Calendar.MINUTE, 0);
        date.set(Calendar.SECOND, 0);
        date.set(Calendar.MILLISECOND, 0);
    }
    protected void beginOfDay() {
        date.set(Calendar.HOUR_OF_DAY, 0);
        beginOfHour();
    }

    protected void beginOfMonth() {
        date.set(Calendar.DAY_OF_MONTH, 1);
        beginOfDay();
    }
    protected void beginOfYear() {
        date.set(Calendar.MONTH, Calendar.JANUARY);
        beginOfMonth();
    }
    protected void now() {
        date = Calendar.getInstance(defaultTimeZone);
    }

}

PARSER_END(DateParser)

SKIP : {" " | "\t" | "\n" | "\r" }

TOKEN :
{
 <NEXT: "next" >
| <PREVIOUS: "previous" >
| <THIS: "this" >
| <POSITIVE: ["0"-"9"] (["0"-"9"])* >
| <THOUSANDS: ["0"-"9"]["0"-"9"]["0"-"9"] >
}


void start() : {}
{
  (date() (["T"] time() (time_zone())?)?)?  (increment() | decrement())* <EOF>
}


void time() : {}
{
    hour_number() ":" minute_number() (":" second_number() ["." millisecond_number()])?
    | "noon"     { beginOfHour(); date.set(Calendar.HOUR_OF_DAY, 12); }
    | "teatime"  { beginOfHour(); date.set(Calendar.HOUR_OF_DAY, 16); }
    | hour_number() "oclock" { date.set(Calendar.MINUTE, 0);}
}

void date() :  {}
{
    LOOKAHEAD(6)
    year_number() "-" month_number() "-" day_number() { beginOfDay(); dynamic = false; }
    | day_of_week() {beginOfDay();}
    | month_of_year() {beginOfMonth();}
    | "now"      { date = Calendar.getInstance(defaultTimeZone); }
    | integer() { date.setTimeInMillis(Long.parseLong(token.toString()) * 1000); dynamic = false; }
    | "today"      { date = Calendar.getInstance(defaultTimeZone); beginOfDay(); }
    | "tomonth"     { date = Calendar.getInstance(defaultTimeZone); beginOfMonth(); }
    | "toyear"      { date = Calendar.getInstance(defaultTimeZone); beginOfYear(); }
    | "tomorrow" { date = Calendar.getInstance(defaultTimeZone); date.add(Calendar.DAY_OF_YEAR, 1); beginOfDay(); }
    | "yesterday" { date = Calendar.getInstance(defaultTimeZone); date.add(Calendar.DAY_OF_YEAR, -1); beginOfDay(); }
    | "yestermonth" { date = Calendar.getInstance(defaultTimeZone); date.add(Calendar.MONTH, -1); beginOfMonth(); }
    | "yesteryear" { date = Calendar.getInstance(defaultTimeZone); date.add(Calendar.YEAR, -1); beginOfYear(); }
    | "duration" { beginOfYear(); date.set(Calendar.YEAR, 1970); }
    | "borreltijd"  { beginOfHour(); date.set(Calendar.HOUR_OF_DAY, 17); date.set(Calendar.DAY_OF_WEEK, Calendar.FRIDAY);
                     if (Calendar.getInstance(defaultTimeZone).after(date)) date.add(Calendar.WEEK_OF_YEAR, 1);

    }
}

void time_zone() : {}
{
    "Z" { date.setTimeZone(TimeZone.getTimeZone("UTC"));}
}


void integer() : {}
{
    ["-"] <POSITIVE>
}



void year_number() : { }
{
    integer()  {  date.set(Calendar.YEAR, Integer.parseInt(token.toString()));}
}
void inc_number() : { }
{
    <POSITIVE> { amount = Integer.parseInt(token.toString()); }
}
void month_number() : { }
{
    <POSITIVE> { date.set(Calendar.MONTH, Integer.parseInt(token.toString()) - 1);}
}
void day_number() : { }
{
    <POSITIVE> { date.set(Calendar.DAY_OF_MONTH, Integer.parseInt(token.toString()));}
}

void hour_number() : { }
{
    <POSITIVE> { date.set(Calendar.HOUR_OF_DAY, Integer.parseInt(token.toString()));}
}
void minute_number() : { }
{
    <POSITIVE> { date.set(Calendar.MINUTE, Integer.parseInt(token.toString())); date.set(Calendar.SECOND, 0); date.set(Calendar.MILLISECOND, 0);}
}
void second_number() : { }
{
    <POSITIVE> { date.set(Calendar.SECOND, Integer.parseInt(token.toString())); date.set(Calendar.MILLISECOND, 0);}
}
void millisecond_number() : { }
{
    <THOUSANDS> { date.set(Calendar.MILLISECOND, Integer.parseInt(token.toString()));}
}


void increment() : {}
{
   "+"  inc_number() inc_period() { date.add(field, amount); amount = 0; }
   | <NEXT> inc_period()  { date.add(field, 1); }
   | <PREVIOUS> inc_period()  { date.add(field, -1); }
   | next() day_of_week() { beginOfDay(); if (date.getTime().getTime() < prev) {date.add(Calendar.DAY_OF_YEAR, 7); } }
   | previous() day_of_week() { beginOfDay(); if (date.getTime().getTime() > prev) {date.add(Calendar.DAY_OF_YEAR, -7); } }
   | <THIS> day_of_week() { beginOfDay(); }
   | next() month_of_year() { beginOfMonth(); if (date.getTime().getTime() < prev) {date.add(Calendar.YEAR, 1); } }
   | previous() month_of_year() { beginOfMonth(); if (date.getTime().getTime() > prev) {date.add(Calendar.YEAR, -1); } }
   | <THIS> month_of_year() { beginOfMonth(); }
   | "ish"  {  date.add(Calendar.SECOND, (int) (random.nextGaussian() * 1200.0)/* 20 min */ + 300 /* 5 minutes late on average*/); dynamic = true;}
}
void decrement() : {}
{
    "-"  inc_number() inc_period() {date.add(field, -1 * amount);}
}
void inc_period() : {}
{
    "second" { field = Calendar.SECOND;}
  | "minute" { field = Calendar.MINUTE;}
  | "hour"   { field = Calendar.HOUR;}
  | "day"    { field = Calendar.DAY_OF_YEAR;}
  | "week"   { field = Calendar.WEEK_OF_YEAR;}
  | "month"  { field = Calendar.MONTH; }
  | "year" {   field = Calendar.YEAR; }
}
void day_of_week() : {}
{
  "sunday" { date.set(Calendar.DAY_OF_WEEK, Calendar.SUNDAY); }
| "monday" { date.set(Calendar.DAY_OF_WEEK, Calendar.MONDAY); }
| "tuesday" { date.set(Calendar.DAY_OF_WEEK, Calendar.TUESDAY); }
| "wednesday" { date.set(Calendar.DAY_OF_WEEK, Calendar.WEDNESDAY); }
| "thursday" { date.set(Calendar.DAY_OF_WEEK, Calendar.THURSDAY); }
| "friday" { date.set(Calendar.DAY_OF_WEEK, Calendar.FRIDAY); }
| "saturday" { date.set(Calendar.DAY_OF_WEEK, Calendar.SATURDAY); }
}
void month_of_year() : {}
{
  "january" { date.set(Calendar.MONTH, Calendar.JANUARY); }
| "february" { date.set(Calendar.MONTH, Calendar.FEBRUARY); }
| "march" { date.set(Calendar.MONTH, Calendar.MARCH); }
| "april" { date.set(Calendar.MONTH, Calendar.APRIL); }
| "may" { date.set(Calendar.MONTH, Calendar.MAY); }
| "june" { date.set(Calendar.MONTH, Calendar.JUNE); }
| "july" { date.set(Calendar.MONTH, Calendar.JULY); }
| "august" { date.set(Calendar.MONTH, Calendar.AUGUST); }
| "september" { date.set(Calendar.MONTH, Calendar.SEPTEMBER); }
| "october" { date.set(Calendar.MONTH, Calendar.OCTOBER); }
| "november" { date.set(Calendar.MONTH, Calendar.NOVEMBER); }
| "december" { date.set(Calendar.MONTH, Calendar.DECEMBER); }
}

void next() : {}
{
    <NEXT> { prev = date.getTime().getTime(); }
}
void previous() : {}
{
    <PREVIOUS> { prev = date.getTime().getTime(); }
}
